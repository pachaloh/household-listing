
PROC GLOBAL

function string staff_segment(string staff_id_code)
	forcase STAFF_DICT where STAFF_ID = staff_id_code do
		staff_segment = STAFF_SEG;
	endfor;
end;

string household_label;

PROC REGION

	
PROC HOUSEHOLDLFS_FF

PROC EA_CODE

preproc
	
	if sysparm("EA_CODE") <> ""  then
		$ = sysparm("EA_CODE");
		REGION   = tonumber($[1:1]);
		DISTRICT = tonumber($[2:2]);
		
		TEAM_CODE = tonumber(sysparm("TEAM_NO"));
		
		if loadcase(EA_DATABASE_DICT,$) then
			ZONE_ID = HEALTH_ZONE_CODE;
			GHAREA  = RESIDENCE_CODE;
		endif;
		
		setproperty($,"Protected", "Yes");
	endif;
PROC GHINTID


preproc

	if sysparm("TEAM_CODE") <> ""  then
		TEAM_CODE = tonumber(sysparm("TEAM_CODE"));
		
		//setproperty($,"Protected", "Yes");
	endif;
	
	if sysparm("GHINTID") <> ""  then
		$ = sysparm("GHINTID");
		
		setproperty($,"Protected", "Yes");
	endif;
	
PROC GHHHNO


preproc
	
	if sysparm("GHHHNO") <> ""  then
		$ = tonumber(sysparm("GHHHNO"));
		
		setproperty($,"Protected", "Yes");
	endif;
PROC HH_SUPERVISOR_CODE

preproc
	
	if sysparm("HH_SUPERVISOR_CODE") <> ""  then
		$ = sysparm("HH_SUPERVISOR_CODE");
		
		setproperty($,"Protected", "Yes");
	endif;
PROC HH_START_INTERVIEW_TIME

preproc

	if HH_START_INTERVIEW_TIME = notappl then
		HH_START_INTERVIEW_TIME = timestamp();
	endif;

	setproperty($, "Protected", "Yes");
PROC HH_INTRODUCTION

if GHVFDT = notappl then
	sysdate("YYYYMMDD");
endif;
PROC SN_STRUCT

preproc
	
	if MULT_HHS = 1 then
		$ = 1;
	else
		$ = 2;
	endif;
	
	noinput;
PROC DESC_STRUCT

if $ = "" then
	errmsg("You cannot leave the field blank");
	reenter;
	
elseif length(strip($)) < 5 then
	errmsg("This doesn't seem like enough for a description! ")
		select("Reenter",reenter,"This is ok", continue);
	
endif;
PROC HH_HEAD

preproc
	
	ask if RES_STRUCT = 1;


postproc
	
	
	if $ = "" then
		errmsg("You cannot leave the field blank");
		reenter;
		
	elseif length(strip($)) < 5 then
		errmsg("This doesn't seem enough for a name!")
			select("Reenter",reenter,"This is ok", continue);
		
	endif;
PROC POPULAR_NAME

preproc
	
	ask if RES_STRUCT = 1;

PROC UNDER_FIVE

preproc
	
	ask if RES_STRUCT = 1;
PROC DU_STATUS


if RES_STRUCT = 1 and $ = 2 then
	errmsg("Confirm that structure belonging to %s is vacant",HH_HEAD)
		select("Reenter",reenter,"This is correct",continue);
endif;
PROC HH_GPS_READING


onfocus

	PollGPS();

	if IsCompleteGPSData() then
		// Update value set
		if getos() in 10,20 then
			setvalueset(HH_GPS_READING, HH_GPS_READING_VS2);
		else
			setvalueset(HH_GPS_READING, HH_GPS_READING_VS4);
		endif;

		// Update CAPI Text
		gps_data_status_capi_text = "collected";
		gps_accuracy_status_capi_label = "GPS accuracy: ";
		if IsDesiredAccuracy(HH_GPS_ACCURACY) then
			gps_accuracy_status_capi_text = "good";

		else
			gps_accuracy_status_capi_text = "bad";
		endif;

		// Preselect option to keep data
		$ = 2;

	else
		// Update value set
		if getos() in 10,20 then
			setvalueset(HH_GPS_READING, HH_GPS_READING_VS1);
		else
			setvalueset(HH_GPS_READING, HH_GPS_READING_VS3);
		endif;

		// Update CAPI Text
		gps_data_status_capi_text = "not collected";
		gps_accuracy_status_capi_label = "";
		gps_accuracy_status_capi_text = "";
		
		
	endif;

postproc

	// Handle user input
	if $ = 1 then
		// Collect GPS data
		numeric result = TryPolledGPSData();
		if not result then
			// Polled GPS reading not used
			TakeGPSReading();
		endif;

	elseif $ = 2 then
		// Skip collecting GPS data, but keep GPS data
		gps_reading_capi_text = "Take GPS reading?";

	elseif $ = 3 then
		// Skip collecting GPS data, and delete GPS data
		if IsCompleteGPSData() then
			warning("GPS data will be deleted.")
				select("Confirm", continue, "Cancel", HH_GPS_READING);
		endif;

		DeleteGPSData();
		gps_reading_capi_text = "Take GPS reading?";

	else
		warning("Unhandled value set option.");
		reenter;
	endif;
PROC HH_LABEL


preproc

	household_label = maketext("OCV/%v/%v",edit("999",GHHHNO),staff_segment(GHINTID));
	
	$ = household_label;

	
	savepartial();
	
	
PROC HH_CONCLUSION

preproc

	savepartial();
	warning("Just make sure you have labelled the household as %s!",maketext("OCV/%v/%v",edit("999",GHHHNO),staff_segment(GHINTID)));


postproc

	if HH_END_INTERVIEW_TIME = notappl then
		HH_END_INTERVIEW_TIME = timestamp();
	endif;
